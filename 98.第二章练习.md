# 第一章  构造数据抽象

## 2.1 数据抽象导引

### 练习2.1

> 请定义出make-rat的一个更好的版本，使之可以处理正数和负数。当有理数为正时，make-rat应当将其规范化，使他的分子和分母都是正的。如果有理数为负，那么就应只让分子为负。

```lisp
(define (make-rat n d)
        (let ((g (gcd (abs n) (abs d))))
             (cond ((> d 0) (cons (/ n g) (/ d g)))
                   ((< d 0) (cons (- (/ n g)) (- (/ d g))))
                   (else (error "error" d)))))
```

* 对于有理数(make-rat n d),当(d>0)时，有理数符号与n相同，(d<0)时，有理数符号与n相反。
* `(gcd (abs n) (abs d))`保证n，d的最大公约数为正。

### 练习2.2

> 请考虑平面上线段的表示问题。一个线段用一对点表示，它们分别是线段的始点与终点。请定义构造函数make-segment和选择函数start-segment、end-segment，它们给予点定义线段的表示。进而，一个点可以用数的序对表示，序对的两个成分分别表示点的x坐标和y坐标。请据此进一步给出构造函数make-point和选择函数x-point、y-point，用它们定义出点的这种表示。最后，请基于所定义的构造函数和选择函数，定义出过程midpoint-segment，他以一个线段为参数，返回线段的中点(也就是那个坐标值是两个端点的平均值的点)。为了实验这些过程，还需要定义一种打印点的方法：

```lisp
(define (print-point p)
        (newline)
        (display "(")
        (display (x-point p))
        (display ",")
        (display (y-point p))
        (display ")"))
```

### 练习2.3

> 请实现一种平面矩形的表示(提示：你有可能借用练习2.2的结果)。基于你的构造函数和选择函数定义几个过程，计算给定矩形的周长和面积等。现在请再为矩形实现另一种表示方式。你应该怎样设计系统，使之能够提供适当的抽象屏障，使同一个周长或者面积过程对两种不同表示都能工作。

要确定平面上矩形的位置和形状，可以给出矩形的4个点或四条边，。每条边都可以用矩形的两个相邻的顶点表示。
假设矩型的四个点分别是ABCD，实际上给出三个点ABC就可以确定第四个点的位置，给出任意两条边也可以确定矩形的位置。
矩形的周长和面积依赖于矩形的长和宽，定义(r-length rect)和(r-width rect)，将矩形的实现与矩形的周长和面积的计算过程隔离开，当矩形的实现修改时，只需要修改(r-length rect)和(r-width rect)的实现。
矩形的构造函数可以使用点(rectangle point-A point-B point-C point-D)；可以使用边(互相平行的两条边)(rectangle segment-AB segment-CD)最后都返回两条边的序对(cons AB CD)
矩形的选择函数可以给出(point-A rect)(point-B rect)(point-C rect)(point-D rect)和边(segment-AB rect)(segment-BC rect)(segment-CD rect)(segment-DA rect)

```lisp
(define (perimeter rect)
        (* 2 (+ (r-length rect) (r-width rect))))

(define (area rect)
        (* (r-length rect) (r-width rect)))
```

### 练习2.4

> 下面是序对的另一种过程性表示方式。请针对这一验证，对于任意的x和y，`(car (cons x y))`都将产生出x。
> ```lisp
> (define (cons x y)
>         (lambda (m) (m x y)))
>
> (define (car z)
>         (z (lambda (p q) p)))
> ```
> 对应的cdr应该如何定义？(提示：为了验证这一表示确实能行，请利用1.1.5节的代换模型。)

```lisp
(car (cons x y))
(car (lambda (m) (m x y)))
((lambda (m) (m x y)) (lambda (p q) p))
((lambda (p q) p) x y)
x
```

```lisp
(define (cdr z)
        (z (lambda (p q) q)))
```

### 练习2.5

> 请证明，如果将a和b的序对表示为乘积$2^a3^b$对应的整数，我们就可以只用非负整数和算术运算表示序对。亲给出对应的过程cons、car和cdr的定义。

对于任意(a,b)都有唯一的$2^a3^b$与之对应。
可以从$2^a3^b$还原出(a,b)
$f(a,b)=2^a3^b$
假设$f(a_1,b_1)f(a_2,b_2) (a_1\not=a_2,b_1\not=b_2)$对应相同的值
$
\begin{aligned}
&2^{a_1}3^{b_1}-2^{a_2}3^{b_2}=0 \\
&2^{a_1}3^{b_1}(1-2^{a_2-a_1}3^{b_2-b_1})=0 \\
&2^{a_2-a_1}3^{b_2-b_1}=1 \\
& \\
&a_1\not=a_2,b_1\not=b_2 \\
&2^{a_2-a_1}3^{b_2-b_1}\not=1
\end{aligned}
$
假设不成立。
$
\begin{aligned}
&f(a,b)=2^a3^b \\
&a=\log_2 {\frac {f(a,b)} {3^b}} \\
&b=\log_3 {\frac {f(a,b)} {2^a}}
\end{aligned}
$

```lisp
(define (cons a b)
        (* (expt 2 a) (expt 3 b)))

(define (car n)
        (define (mod3 x)
                (if (= (mod x 3) 0)
                    (mod3 (/ x 3))
                    x))
        (log (mod3 n) 2))

(define (cdr n)
        (define (mod2 x)
                (if (= (mod x 2) 0)
                    (mod2 (/ x 2))
                    x))
        (log (mod2 n) 3))
```

### 练习2.6

> 如果觉得将序对表示为过程还不足以令人如雷灌顶，那么请考虑，在一个可以对过程做各种操作的语言里，我们完全可以没有数(至少在只考虑非负整数的情况下)，可以将0和加1操作实现为：
> ```lisp
> (define zero (lambda (f) (lambda (x) x)))
> 
> (define (add-1 n)
>         (lambda (f) (lambda (x) (f ((n f) x)))))
> ```
> 这一表示形式称为Church计数，名字来源于其发明人数理逻辑学家Alonzo Church(丘奇),$\lambda$演算也是他发明的。
请直接定义one和two(不用zero和add-1)(提示：利用代换去求值(add-1 zero))。请给出加法过程+的一个直接定义(不要通过反复应用add-1)。

```lisp
(add-1 zero)
(add-1 (lambda (f) (lambda (x) x)))
(lambda (g) (lambda (y) (g ((n g) y))))
(lambda (g) (lambda (y) (g (((lambda (f) (lambda (x) x)) g) y))))
(lambda (g) (lambda (y) (g (y))))
(lambda (f) (lambda (x) (f x)))

(define one (lambda (f) (lambda (x) (f x))))
(define two (lambda (f) (lambda (x) (f f(x)))))

(define (add a b)
        (lambda (f)
                (lambda (x)
                        (b (f (a f) x)))))
```

zero没有参数，返回一个过程，这个过程接受一个参数f，返回一个对这个参数什么也不做的过程。
add-1接受一个参数n,返回一个过程，该过程也接受一个参数f，返回另一个过程，该过程接受一个参数x，并进行如下操作：1.将n应用于f得到一个过程，2.将得到的这个过程应用于x，3，将f应用于2得到的结果。

当定义了0和+1两个过程后，就可以由这两个过程描述出1和1之后的所有数。add-1过程中的关键步骤(n f)，n可以表述为(add-1 (add-1 (add-1 ...(add-1 zero))))的一个递归过程。

### 练习2.7

> Alyssa的程序是不完整的，因为她还没有确定区间抽象的实现。这里是区间构造符的定义：
> `(define (make-interval a b) (cons a b))`
> 请定义选择符upper-bound和lower-bound，完成这一实现。

```lisp
(define (upper-bound n)
        (max (car n) (cdr n)))

(define (lower-bound n)
        (min (car n) (cdr n)))
```

### 练习2.8

> 通过类似于Alyssa的推理，说明两个区间的差应该怎样计算。请定义出相应的减法过程sub-interval。

```lisp
(define (sub-interval a b)
        (make-interval (- (lower-bound a) (upper-bound b))
                       (- (upper-bound a) (lower-bound b))))
```

### 练习2.9

> 区间的宽度就是其上界和下界之差的一半。区间宽度是有关区间所描述的相应数值的非确定性的一种度量。对于某些算术运算，两个区间的组合结果的宽度就是参数区间参数的函数，而对其他运算，组合区间的宽度则不是区间宽度的函数。证明两个区间的和(与差)的宽度就是被加(或减)的区间的宽度的函数。举例说明，对于乘和除而言，情况并非如此。

$
\begin{aligned}
&a=(a_1,a_2) \\
&b=(b_1,b_2) \\
&a_length=(a_2-a_1)/2 \\
&b_length=(b_2-b_1)/2 \\
&m=a+b=(a_1+b_1,a_2+b_2) \\
&n=a-b=(a_1-b_2,a_2-b_1) \\
&m_length=(a_2+b_2-(a_1+b_1))/2=a_length+b_length \\
&n_length=(a_2-b_1-(a_1-b_2))/2=a_length+b_length \\
&a=(1,3) \to length=1\\
&b=(2,10) \to length=4\\
&ab=(2 30) \to length=14 \\
&a=(2,4) \to length=1 \\
&b=(3 11) \to length=4 \\
&ab=(6,44) \to length=19
\end{aligned}
$

### 练习2.10

> BenBitdiddle是个专业程序员，他看了Alyssa工作后评论说，除以一个跨过横跨0的区间的意义不清楚。请修改Alyssa的代码，检查这种情况并在出现这一种情况时报错。

```lisp
(define (div-interval x y)
        (if (<= (* (upper-bound y) (lower-bound y)) 0)
            (error "y" y)
            (mul-interval x
                           (make-interval (/ 1.0 (upper-bound y))
                                          (/ 1.0 (lower-bound y))))))
```

### 练习2.11

> 在看了这些东西之后，Ben又说出了下面这段有些神秘的话:"通过监测区间的端点，有可能将mul-interval分解为9种情况，每种情况中所需的乘法都不超过两次"。请根据Ben的建议重写这个过程。

$
\begin{aligned}
&a=(a_l,a_u) \\
&b=(b_l,b_u) \\
&(1)a_l \geqslant 0,b_l \geqslant 0\\
&(2)a_l \geqslant 0,b_l \lt 0,b_u \geqslant 0 \\
&(3)a_l \geqslant 0,b_l \lt 0,b_u \lt 0  \to a_l \geqslant 0,b_u \lt 0\\
&(4)a_l \lt 0,a_u \geqslant 0,b_l \geqslant 0 \\
&(5)a_l \lt 0,a_u \lt 0,b_l \geqslant 0 \to a_u \lt 0,b_l \geqslant 0 \\
&(6)a_l \lt 0,a_u \geqslant 0,b_l \lt 0,b_u \geqslant 0 \\
&(7)a_l \lt 0,a_u \geqslant 0,b_l \lt 0,b_u \lt 0 \to a_l \lt 0,a_u \geqslant 0,b_u \lt 0\\
&(8)a_l \lt 0,a_u \lt 0,b_l \lt 0,b_u \geqslant 0 \to a_u \lt 0,b_l \lt 0,b_u \geqslant 0\\
&(9)a_l \lt 0,a_u \lt 0,b_l \lt 0,b_u \lt 0 \to a_u \lt 0,b_u \lt 0\\
\end{aligned}
$

### 练习2.12

> 请定义一个构造函数make-center-percent，他以一个中心点和一个百分比为参数，产生出所需要的区间。你还需要定义选择函数percent，通过它可以得到给定区间的百分数误差，选择函数center与前面定义的一样。

> 区间的宽度就是其上界和下界之差的一半。(练习2.9)

```lisp
(define (make-center-percent c p)
        (make-interval (+ c (* c p))
                       (- c (* c p))))

(define (percent i)
        (/ (- (upper-bound i) (lower-bound i))
           2
           (center i)))
```

### 练习2.13

> 请证明，在误差为很小的百分数的条件下，存在着一个简单公式，利用它可以从两个被乘区间的误差算出乘积的百分数误差值。你可以假定所有的数为正，以简化这一问题。

$
\begin{aligned}
ab&=(a_{l}b_{l},a_{u}b_{u}) \\
&=(a(1-p_{a})b(1-p_{b}),a(1+p_{a})b(1+p_{b})) \\
&=(ab(1-p_{a}-p_{b}+p_{a}p_{b}),ab(1+p_{a}p_{b}+p_{a}+p_{b})) \\
&=(ab(1+p_{a}p_{b}-(p_{a}+p_{b})),ab(1+p_{a}p_{b}+(p_{a}+p_{b})))) \\
w_{ab}&=(ab(1+p_{a}p_{b}+(p_{a}+p_{b}))-ab(1+p_{a}p_{b}-(p_{a}+p_{b})))/2 \\
&=ab(2(p_{a}+p_{b}))/2 \\
&=ab(p_{a}+p_{b}) \\
c_{ab}&=(ab(1+p_{a}p_{b}+(p_{a}+p_{b}))+ab(1+p_{a}p_{b}-(p_{a}+p_{b})))/2 \\
&=ab(2+2p_ap_b)/2 \\
&=ab(1+p_ap_b) \\
p_{ab}&=w_{ab}/c_{ab} \\
&=ab(p_a+p_b)/ab(1+p_ap_b) \\
&=\frac {p_a+p_b} {1+p_ap_b}
\end{aligned}
$

### 练习2.14

> 请确认Lem是对的。请你用各种不同的算术表达式来检查这一系统的行为。请做出两个区间A和B，并用他们计算表达式A/A和A/B。如果所用区间的宽度对于中心值取很小百分数，你将会得到更多的认识。请检查对于中心-百分比形式(见练习2.12)进行计算的结果。

$A/A\not= (1,1)$

### 练习2.15

> 另一用户Eva Lu Ator也注意到了由不同的等价代数表达式计算出的区间的差异。她说，如果一个公式可以写成一种形式，其中具有非准确性的变量不重复出现，那么Alyssa的系统产生出的区间的限界更紧一些。她说，在计算并联电阻时，par2是比par1"更好的"程序。她说的对吗？

par2能得到正确的结果。但Eva的解释不对。当R1,R2都去最小值时，并联电阻值为最小值，当R1，R2都取最大值时，并联电阻取最大值，即$R(r1,r2)=(\frac{R1_lR2_l} {R1_l+R2_l},\frac{R1_uR2_u} {R1_u+R2_u})$

### 练习2.16

> 请给出一个一般性的解释：为什么等价的代数表达式可能导致不同计算结果？你能设计出一个区间算术包，使之没有这种缺陷吗？或者这个事情根本不可能做到？(警告：这个问题非常难。)

Alyssa给出的实现中(div-interval)和(mul-interval)不是逆运算。
对于实际运算(-2,3)*(4,5)=(-10,15)=(-2,3)*(3,5)也不是逆运算，因此区间运算不满足数的运算的规律，因此数的运算中等价的代数表达式对于区间运算而言不一定等价。在练习2.11中，可以看到，做区间乘法时，不一定会用到区间的四个边界。

### 练习2.17

> 请定义出过程last-pair,它返回只包含给定(非空)表里最后一个元素表：
> ```lisp
> (last-pair (list 23 72 149 34))
> (34)
> ```

```lisp
(define (last-pair items)
        (let ((p (- (length items) 1)))
             (list (list-ref items p))))
```

### 练习2.18

> 请定义出过程reverse,它以一个表为参数，返回的表中所包含的元素与参数表相同，但排列顺序与参数表相反：
> ```lisp
> (reverse (list 1 4 9 16 25))
> (25 16 9 4 1)
> ```

```lisp
(define (reverse items)
        (define (iter r next)
                (if (null? next)
                    r
                    (iter (cons (car next) r) (cdr next))))
        (iter (list) items))
```

### 练习2.19

> 请考虑1.2.2节的兑换零钱计数程序。如果能轻而易举地改变程序里所用的兑换币种就更好了。譬如说，那样我们就能计算出1英镑的不同兑换方式的数目。在写前面那个程序时，有关币种的知识中有一部分出现在过程first-denomination里，另一部分出现在过程count-change(它知道有5种U.S.硬币)。如果能够用一个表来提供可用于兑换的硬币就好了。
我们希望重写出过程cc，使其第二个参数是一个可用硬币的币值表，而不是一个制定可用硬币种类的整数。而后我们就可以针对各种货币定义出一些表：
> ```lisp
> (define us-coins (list 50 25 10 5 1))
> (define uk-coins (list 100 50 20 10 5 2 1 0.5))
> ```
> 然后我们就可以通过如下方式调用cc：
> ```lisp
> (cc 100 us-coins)
> 292
>```
> 为了做到这件事，我们需要对程序cc做一些修改。他仍然具有同样的形式，但将以不同的方式访问自己的第二个参数，如下面所示：
> ```lisp
> (define (cc amount coin-values)
>         (cond ((= amount 0) 1)
>               ((or (< amount 0) (no-more? coin-values)) 0)
>               (else
>                (+ (cc amount 
>                       (except-first-denomination coin-values))
>                   (cc (- amount (first-denomination coin-values))
>                       coin-values)))))
> ```
> 请基于表结构上的基本操作，定义出过程first-denomination、except-first-denomination和no-more?。表coin-values的排列顺序会影响cc给出的回答吗？为什么?

```lisp
(define (first-denomination coin-values)
        (car coin-values))

(define (except-first-denomination coin-values)
        (cdr coin-values))

(define (no-more? coin-values)
        (null? coin-values))
```
coin-values的顺序不影响结果。

### 练习2.20

> 过程+、*和list可以取任意个数的实际参数。定义这类过程的一种方式是用一种带点尾部记法形式的define。在一个过程定义中，如果在形式参数表的最后一个参数之前有一个点号，那就表明，当这一过程被实际调用时，前面各个形式参数(如果有的话)将以前面的各个实际参数为值，与平常一样。但最后一个形式参数将以所有剩下的实际参数的表为值。例如，假若我们定义了：
> `(define (f x y . z) <body>)`
> 过程f就可以用两个以上的参数调用。如果求值：
> `(f 1 2 3 4 5 6)`
> 那么在f的体里，x将是1，y将是2，而z将是表(3 4 5 6)。给了定义：
> `(define (g . w) <body>)`
> 过程g可以用0个或者多个参数调用。如果求值：
> `(g 1 2 3 4 5 6)`
> 那么在g的体里，w将是表(1 2 3 4 5 6)。
请采用这一记法形式写出过程same-parity，他以一个或者多个整数为参数，返回所有与其第一个参数有着同样奇偶性的参数形成的表。例如；
> ```lisp
> (same-parity 1 2 3 4 5 6 7)
> (1 3 5 7)
> (same-parity 2 3 4 5 6 7)
> (2 4 6)
> ```

```lisp
(define (same-parity x . y)
        (define (same a b)
                (if (even? a)
                    (if (even? b)
                        (list b)
                        (list))
                    (if (even? b)
                        (list)
                        (list b))))
        (define (iter r next)
                (if (null? next)
                    r
                    (iter (append r (same x (car next))) (cdr next))))
        (iter (list x) y))
```

### 练习2.21

> 过程square-list以一个数值表为参数，返回每个数的平方构成的表：
> ```lisp
> (square-list (list 1 2 3 4))
> (1 4 9 16)
> ```
> 下面是square-list的两个定义，请填充其中缺少的表达式以完成它们：
> ```lisp
> (define (square-list items)
>         (if (null? items)
>             nil
>             (cons <??> <??>)))
> (define (square-list items)
>         (map <??> <??>))
> ```

```lisp
(define (square-list items)
         (if (null? items)
             nil
             (cons (square (car items)) (square-list (cdr items)))))

(define (square-list-map items)
        (map square items))
```

### 练习2.22

> Louis Reasoner试图重写练习2.21的第一个square-list过程，希望使它能生成一个迭代计算过程：
> ```lisp
> (define (square-list items)
>         (define (iter things answer)
>                 (if (null? things)
>                     answer
>                     (iter (cdr things)
>                           (cons (square (car things))
>                                 answer))))
>         (iter items nil))
> ```
> 但是很不幸，在按这种方式定义出的square-list产生出的结果表中，元素的顺序正好与我们需要的相反。为什么？
Loius又试着修正其程序，交换了cons的参数：
> ```lisp
> (define (square-list items)
>         (define (iter things answer)
>                 (if (null? things)
>                     answer
>                     (iter (cdr things)
>                           (cons answer
>                                 (square (car things))))))
>         (iter items nil))
> ```
> 但还是不行。请解释为什么。

```lisp
(iter (1 2 3 4) nil)
(iter (2 3 4) (cons 1 nil))
(iter (2 3 4) (1))
(iter (3 4) (cons 4 (1)))
(iter (3 4) (4 1))
...
(16 9 4 1)

(iter (1 2 3 4) nil)
(iter (2 3 4) (cons nil 1))
(iter (2 3 4) (1))
(iter (3 4) (cons (1) 4))
(iter (3 4) ((1) 4))
...
((((1) 4) 9) 16)
```

### 练习2.23

> 过程for-each与map类似，它以一个过程和一个元素表为参数，但它并不返回结果的表，只是将这一过程从左到右应用于各个元素，将过程应用于元素得到的值都丢掉不用。for-each通常用于那些执行了某些动作的过程，如打印等。看下面例子：
> ```lisp
> (for-each (lambda (x) 
>                   (newline)
>                   (display x))
>           (list 57 321 88))
> ```
> for-each的调用返回的值(上面没有显示)可以是某种任意的东西，例如逻辑值真。请给出一个for-each的实现。

```lisp
(define (for-each f items)
        (define (f-run n)
                (f n)
                f)
        (define (iter r next)
                (if (null? next)
                    f
                    (iter (f-run (car next)) (cdr next))))
        (iter f-run items))
```

### 练习2.24

> 假定现在要求值表达式(list 1 (list 2 (list 3 4)))，请给出由解释器打印出的结果，给出与之对应的盒子指针结构，并将它解释为一棵树。(参见图2-6)。

`(1 (2 (3 4)))`

### 练习2.25

> 给出能够从下面各表中取出7的car和cdr组合：
> ```lisp
> (1 3 (5 7) 9)
> ((7))
> (1 (2 (3 (4 (5 (6 7)))))
> ```

```lisp
(car (cdr (car (cdr (cdr a)))))
(car (car b))
(car (cdr (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr c))))))))))))
```

```lisp
(cdr list) 返回一个序对
(cdr (list 1 2))
=>(2)<=>(list 2)
(cdr (list 1 (list 2)))
=>((2))<=>(list (list 2))
```

### 练习2.26

> 假定已将x和y定义为如下的两个表：
`(define x (list 1 2 3))`
`(define y (list 4 5 6))`
解释器对于下面各个表达式将打印出什么结果：
`(append x y)`
`(cons x y)`
`(list x y)`

```lisp
(append x y)
=>(1 2 3 4 5 6)
(cons x y)
=>((1 2 3) 4 5 6)
(list x y)
((1 2 3) (4 5 6))
```

(append x y):表x和表y的元素组合成一个新表(将表y的元素顺序添加到表x之后)
(append (list 1 2) (list 3 (list 4 5)))
=>(1 2 3 (4 5))
(cons x y):在表y之前增加一个元素(表x)
(list x y):将表x和表y组合成新的表(x y)(表x，y作为新表的元素)

### 练习2.27

> 修改练习2.18中所做的reverse过程，得到一个deep-reverse过程。他以一个表为参数，返回另一个表作为值，结果表中的元素反转过来，其中的子树也反转。例如：
> ```lisp
> (define x (list (list 1 2) (list 3 4)))
> x
> ((1 2) (3 4))
> (reverse x)
> ((3 4) (1 2))
> (deep-reverse x)
> ((4 3) (2 1))
> ```

```lisp
(define (deep-reverse items)
        (define (iter r next)
                (if (pair? next)
                    (append (iter r (cdr next)) (list (iter r (car next))))
                    next))
        (iter (list) items))
```

### 练习2.28

> 写一个过程fringe，他以一个树(表示为表)为参数，返回一个表，表中的元素是这棵树的所有树叶，按照从左到右的顺序。例如：
> ```lisp
> (define x (list (list 1 2) (list 3 4)))
> (fringe x)
> (1 2 3 4)
> (fringe (list x x))
> (1 2 3 4 1 2 3 4)
> ```

```lisp
(define (fringe items)
        (define (append-not-pair i)
                (if (null? i)
                    i
                    (list i)))
        (define (iter r next)
                (if (pair? next)
                    (append (iter r (car next)) (iter r (cdr next)))
                    (append-not-pair next)))
        (iter (list) items))
```

### 练习2.29

> 一个二叉活动体由两个分支组成，一个是左分支，另一个是右分支。每个分支是一个具有确定长度的杆，上面或者吊着一个重量，获证吊着另一个二叉活动体。我们可以用复合数据对象表示这种二叉活动体，将它通过其两个分支构造起来(例如，使用list):
> ```lisp
> (define (make-mobile left right)
>         (list left right))
> ```
> 分支可以从一个length(他应该是一个数)再加上一个structure构造出来，这个structure或者是一个数(表示一个简单重量)，或者是另一个活动体：
> ```lisp
> (define (make-branch length structure)
>         (list length structure))
> ```
> a)请写出相应的选择函数left-branch和right-branch，它们分别返回活动体的两个分支。还有branch-length和branch-structure，它们返回一个分支上的成分。
b)用你的选择函数定义过程total-weight，它返回一个活动体的总重量。
c)一个活动体称为是平衡的，如果其左分支的力矩等于其右分支的力矩(也就是说，如果其左杆长度乘以吊在杠上的重量，等于这个活动体右边的同样乘积)，而且在其每个分支上吊着的子活动体也都平衡。请设计一个过程，他能检查一个二叉活动体是否平衡。
d)假定我们改变活动体的表示，采用下面的构造方式：
> ```lisp
> (define (make-mobile left right)
>         (cons left right))
> (define (make-branch length structure)
>         (cons length structure))
> ```
> 你需要对自己的程序做多少修改，才能将它改为使用这种新表示？

```lisp
(define (total-weight mobile)
        (define (have-mobile? branch)
                (if (pair? branch)
                    (pair? (branch-structure branch))
                    #f))
        (define (branch-weight branch)
                (if (have-mobile? branch)
                    (total-weight (branch-structure branch))
                    (branch-structure branch)))
        (+ (branch-weight (left-branch mobile))
           (branch-weight (right-branch mobile))))

(define (balance? mobile)
        (define (have-mobile? branch)
                (if (pair? branch)
                    (pair? (branch-structure branch))
                    #f))
        (define (branch-weight branch)
                (if (have-mobile? branch)
                    (total-weight (branch-structure branch))
                    (branch-structure branch)))
        (define (left-moment m)
                (* (branch-length (left-branch m))
                   (branch-weight (left-branch m))))
        (define (right-moment m)
                (* (branch-length (right-branch m))
                   (branch-weight (right-branch m))))
        (define (balance-branch? branch)
                (let ((next (branch-structure branch)))
                     (if (have-mobile? branch)
                         (balance? next)
                         #t)))
        (if (pair? mobile)
            (if (= (left-moment mobile) (right-moment mobile))
                (and (balance-branch? (left-branch mobile)) (balance-branch? (right-branch mobile)))
                #f)
            #t))
```

### 练习2.30

> 请定义一个与练习2.21中square-list过程类似的square-tree过程。也就是说，他应该具有下面的行为：
> ```lisp
> (square-tree
>    (list 1
>          (list 2 (list 3 4) 5)
>          (list 6 7)))
> (1 (4 (9 16) 25) (36 49))
> ```
> 请以两种方式定义square-tree，直接定义(即不适用任何高阶函数)，以及使用map和递归定义。

```lisp
(define (square-tree1 tree)
        (map (lambda (sub-tree)
                     (if (pair? sub-tree)
                         (square-tree1 sub-tree)
                         (square sub-tree)))
              tree))

(define (square-tree2 tree)
        (cond ((null? tree) tree)
              ((not (pair? tree)) (square tree))
              (else (cons (square-tree2 (car tree))
                          (square-tree2 (cdr tree))))))
```

### 练习2.31

> 将你在练习2.30做出的解答进一步抽象，做出一个过程，使他的性质能保证以下面的形式定义square-tree：
`(define (square-tree tree) (tree-map square tree))`

```lisp
(define (tree-map proc tree)
        (map (lambda (sub-tree)
                     (if (pair? sub-tree)
                         (tree-map proc sub-tree)
                         (proc sub-tree)))
              tree))
```

### 练习2.32

> 我们可以将一个集合表示为一个元素互不相同的表，因此就可以将一个集合的所有子集表示为表的表。例如，假定集合为(1 2 3)，它的所有子集的集合就是(() (3) (2) (2 3) (1) (1 3) (1 2) (1 2 3))。请完成下面的过程定义，他生成一个集合的所有子集的集合。请解释它为什么能完成这一工作。
> ```lisp
> (define (subsets s)
>         (if (null? s)
>             (list (list))
>             (let ((rest (subsets (cdr s))))
>                  (append rest (map <??> rest)))))
> ```

```lisp
(define (subsets s)
        (if (null? s)
            (list nil)
            (let ((rest (subsets (cdr s))))
                 (append rest (map (lambda (x)
                                           (cons (car s) x))
                                   rest)))))
```

rest：不包含元素(car s)的s的子集((cdr s)的子集)
`(map (lambda (x) (cons (car s) x)) rest)`：包含元素(car s)的子集。(将rest的每个元素与(car s)组合成新的集合)

### 练习2.33

> 请填充下面缺失的表达式，完成将一些基本的表操作看作累积的定义：
> ```lisp
> (define (map p sequence)
>         (accumulate (lambda (x y) <??>) nil sequence))
> (define (append seq1 seq2)
>         (accumulate cons <??> <??>))
> (define (length sequence)
>         (accumulate <??> 0 sequence))
> ```

```lisp
(define (map p sequence)
        (accumulate (lambda (x y) (cons (p x) y)) 
                    (list)
                    sequence))

(define (append seq1 seq2)
        (accumulate cons seq2 seq1))

(define (length sequence)
        (accumulate (lambda (x y)
                            (+ 1 y)) 
                    0
                    sequence))
```

### 练习2.34

> 对于x的某个给定值，求出一个多项式在x的值，也可以形式化为一种累积。假定需要求下面多项式的值：
$a_nx^n+a_{n-1}x^{n-1}+...+a_1x+a_0$
采用著名的Horner规则，可以构造出下面的计算：
$(...(a_nx+a_n)x+...+a_1)x+a_0$
换句话说，我们可以从$a_n$开始，乘以x，再加上$a_{n-1}$,乘以x，如此下去，直到处理完$a_0$。请填充下面的模板，做出一个利用Horner规则求值多项式的过程。假定多项式的系数安排在一个序列里，从$a_0$直至$a_n$
> ```lisp
> (define (horner-eval x coefficient-sequence)
>         (accumulate (lambda (this-coeff higher-terms) <??>)
>                     0
>                     coefficient-sequence))
> ```
> 例如为了计算$1+3x+5x^3+x^5$在$x=2$的值，你需要求值：
`(horner-eval 2 (list 1 3 0 5 0 1))`

```lisp
(define (horner-eval x coefficient-sequence)
        (accumulate (lambda (this-coeff higher-terms)
                            (+ this-coeff
                               (* x higher-terms)))
                    0
                    coefficient-sequence))
```

### 练习2.35

> 将2.2.2节的count-leaves重新定义为一个累积：
> ```lisp
> (define (count-leaves t)
>         (accumulate <??> <??> (map <??> <??>))
> ```

```lisp
(define (count-leaves t)
        (accumulate (lambda (x y) (+ x y)) 
                    0
                    (map (lambda (sub-tree)
                                 (if (pair? sub-tree)
                                     (count-leaves sub-tree)
                                     1))
                         t)))
```

```lisp
;;子树的叶子数
(lambda (sub-tree)
        (if (pair? sub-tree)
            (count-leaves sub-tree)
            1))
```

### 练习2.36

> 过程accumulate-n与accumulate类似，除了它的第三个参数是一个序列的序列，假定其中每个序列的元素个数相同。它用指定的积累过程去组合起所有序列的第一个元素，而后是所有序列的第二个元素，并如此做下去，返回得到的所有结果的序列。例如，如果s是包含着4个序列的序列((1 2 3) (4 5 6) (7 8 9) (10 11 12))，那么(accumulate + 0 s)的值就应该是序列(22 26 30)。请填充下面accumulate-n定义中所缺失的表达式：
> ```lisp
> (define (accumulate-n op init seqs)
>         (if (null? (car seqs))
>             (list)
>             (cons (accumulate op init <??>)
>                   (accumulate-n op init <??>))))
> ```

```lisp

(define (accumulate-n op init seqs)
        (if (null? (car seqs))
            (list)
            (cons (accumulate op init (map car seqs))
                  (accumulate-n op init (map cdr seqs)))))
```

`(accumulate op init (map car seqs)`:序列第一个元素组成的新序列的累积

### 练习2.37

> 嘉定我们将向量$v=(v_i)$表示为数的序列，将矩阵$m=(m_{ij})$表示为向量(矩阵行)的序列。例如，矩阵：
$
\begin{vmatrix}
1&2&3&4 \\
4&5&6&6 \\
6&7&8&9
\end{vmatrix}
$
用序列((1 2 3 4) (4 5 6 6) (6 7 8 9))表示。对于这种表示，我们可以用序列操作简洁地表达基本的矩阵与向量运算。这些运算(任何有关矩阵代数的书里都有描述)如下：
(dot-product v w)  返回和$\sum_iv_iw_i$;
(matrix-*-vector m v) 返回向量t，其中：$t_i=\sum_jm_{ij}v_j$;
(matrix-*-matrix m n) 返回矩阵p，其中：$p_{ij}=\sum_km_{ik}n{kj}$
(transpose m) 返回矩阵n，其中: $n_{ij}=m_{ji}$
我们可以将点积(dot product)定义为：
> ```lisp
> (define (dot-product v w)
>         (accumulate + 0 (map * v w)))
> ```
> 请填充下面过程里缺失的表达式，它们计算出其他的矩阵运算结果(过程accumulate-n在练习2.36中定义)。
> ```lisp
> (define (matrix-*-vector m v)
>         (map <??> m))
> (define (transpose mat)
>         (accumulate-n <??> <??> mat))
> (define (matrix-*-matrix m n)
>         (let ((cols (transpose n)))
>              (map <??> m)))
> ```

```lisp
(define (matrix-*-vector m v)
        (map (lambda (x)
                     (dot-product x v))
             m))

(define (transpose mat)
        (accumulate-n cons (list) mat))

(define (matrix-*-matrix m n)
        (let ((cols (transpose n)))
             (map (lambda (x)
                          (accumulate cons
                                      (list)
                                      (map (lambda (y)
                                                   (dot-product x y))
                                            cols))) m)))
```

### 练习2.38

> 过程accumulate也被称为fold-right，因为它将序列的第一个元素组合到右边所有元素的组合结果上。也有一个fold-left，它与fold-right类似，但却是按照相反方向去操作各个元素：
> ```lisp
> (define (fold-left op initial sequence)
>         (define (iter result rest)
>                 (if (null? rest)
>                     result
>                     (iter (op result (car rest))
>                           (cdr rest))))
>        (iter initial sequence))
> ```
> 下面各表达式的值是什么？
> ```lisp
> (fold-right / 1 (list 1 2 3))
> (fold-left / 1 (list 1 2 3))
> (fold-right list nil (list 1 2 3))
> (fold-left list nil (list 1 2 3))
> ```
> 如果要求某个op时保证fold-right和fold-left对任何序列都产生同样的结果，请给出op应该满足的性质。

1:3/2
(a / 1 (1 2 3))
(/ 1 (a / 1 (2 3)))
(/ 1 (/ 2 (a / 1 (3))))
(/ 1 (/ 2 (/ 3 (a / 1 nil))))
(/ 1 (/ 2 (/ 3 1)))
(/ 1 (/ 2 3))
(/ 1 2/3)
(3/2)
2:1/6
(iter 1 (1 2 3))
(iter (/ 1 1) (2 3))
(iter 1 (2 3))
(iter (/ 1 2) (3))
(iter 1/2 (3))
(iter (/ 1/2 3) ())
(iter (1/6) ())
(1/6)
3:(1 (2 (3 ())))
(a list () (1 2 3))
(list 1 (a list () (2 3)))
(list 1 (list 2 (a list () (3))))
(list 1 (list 2 (list 3 (a list () ()))))
(list 1 (list 2 (list 3 ()))
(list 1 (list 2 (3 ())))
(list 1 (2 (3 ())))
(1 (2 (3 ())))
4:(((() 1) 2) 3)
(iter () (1 2 3))
(iter (list () 1) (2 3))
(iter (() 1) (2 3))
(iter (list (() 1) 2) (3))
(iter ((() 1) 2) (3))
(iter (list ((() 1) 2) 3) ())
(iter (((() 1) 2) 3) ())
(((() 1) 2) 3)
(op x y)=(op y x)

### 练习2.39

> 基于练习2.38的fold-right和fold-left完成reverse(练习2.18)下面的定义：
> ```lisp
> (define (reverse sequence)
>         (fold-right (lambda (x y) <??>) nil sequence))
> (define (reverse sequence)
>         (folde-left (lambda (x y) <??>) nil sequence))
> ```

```lisp
(define (reverse1 sequence)
        (fold-right (lambda (x y)
                            (append y (list x))) (list) sequence))

(define (reverse2 sequence)
        (fold-left (lambda (x y)
                            (append (list y) x)) (list) sequence))
```

(op x y)在fold-right中，x表示左值(左侧元素)，y表示对x右边所有值应用op的结果；在fold-left中，x表示对y左边所有值应用op的结果，y表示右值(右侧元素)。

### 2.40

> 请定义过程unique-pairs,给它整数n，它产生出序对(i,j)，其中$1\leqslant j\leqslant i\leqslant n$。请用unique-pairs去简化上面prime-sum-pairs的定义。

```lisp
(define (unique-pairs n)
        (flatmap (lambda (i)
                         (map (lambda (j)
                                      (list i j))
                                      (enumerate-interval (+ i 1) n)))
                 (enumerate-interval 1 n)))

(define (unique-pairs n)
        (flatmap (lambda (i)
                         (map (lambda (j)
                                      (list i j))
                                      (enumerate-interval 1 (- i 1))))
                 (enumerate-interval 1 n)))

(define (prime-sum-pairs n)
        (map make-pair-sum
             (filter prime-sum? (unique-pairs n))))
```

### 练习2.41

> 请定义一个过程，它能产生出所有小于等于给定整数n的正的相异正整数i，j，k的有序三元组，使每一个三元组的三个元之和等于给定的整数s。

```lisp
(define (unique-triples n)
        (flatmap (lambda (x)
                         (map (lambda (y)
                                      (cons x y))
                              (unique-pairs (- x 1))))
                 (enumerate-interval 1 n)))


(define (ns-pairs n s)
        (define (sum-eq-s? l)
        (= (+ (car l) (cadr l) (caddr l))
          s))
        (filter sum-eq-s? (unique-triples n)))
```

### 练习2.42

> "八皇后谜题"问的是怎样将八个皇后摆在国际象棋盘上，使得任意一个皇后都不能攻击另一个皇后(也就是说，任意两个皇后都不在同一行、同一列或者同一对角线上)。一个可能的解如图2-8所示。解决这一谜题的一种方法按一个方向处理棋盘，每次在每一列里放一个皇后。如果现在已经放好了k-1个皇后，第k个皇后就必须放在不会被已在棋盘上的任何皇后攻击的位置上。我们可以递归地描述这一过程：假定我们已经生成了在棋盘上的前k-1列中放置k-1个皇后的所有可能方式，现在需要的就是对其中的每种方式，生成出将下一个皇后放在第k列中每一行的扩充集合。而后过滤它们，只留下能使位于第k列的皇后与其它皇后相安无事的那些扩充。这样就能产生出将k个皇后放置在前k列的所有格局的序列。继续这一过程，我们就能产生出这一谜题的所有解，而不是一个解。
将这一解法实现为一个过程queens，令它返回在n*n棋盘上放n个皇后的所有解的序列。queens内部的过程queen-cols，返回在棋盘的前k列中放皇后的所有格局的序列。
> ```lisp
> (define (queens board-size)
>         (define (queen-cols k)
>                 (if (= k 0)
>                     (list enpty-board)
>                     (filter (lambda (positions) (safe? k positions))
>                             (flatmap (lambda (rest-of-queens)
>                                              (map (lambda (new-row)
>                                                           (adjoin-position new-row k rest-of-queens))
>                                                   (enumerate-interval 1 board-size))
>                                      (queen-cols (- k 1)))))))
>         (queen-cols board-size))
> ```
> 这个过程里的rest-of-queens是在前k-1列放置k-1个皇后的一种方式，new-row是在第k列放置所考虑的行编号。请完成这一程序，为此需要实现一种棋盘格局集合的表示方式，还要实现过程adjoin-position,它将一个新的行列格局加入一个格局集合；empty-board，它表示空的格局集合。你还需要写出过程safe？，它能确定在一个格局中，在第k列的皇后相对于其它列的皇后是否为安全的(请注意，我们只需检查新皇后是否安全——其他皇后已经保证相安无事了)。

```lisp
(define empty-board `())

(define (adjoin-position new-row k rest-of-queens)
        (cons new-row rest-of-queens))

(define (safe? k positions)
        (let ((num (car positions)))
              (define (iter-safe? n items r)
                      (if (not r)
                          #f
                          (if (null? items)
                              #t
                              (iter-safe? (+ 1 n) 
                                          (cdr items) 
                                          (and (not (= (car items) (+ num n)))
                                               (not (= (car items) (- num n)))
                                               (not (= (car items) num)))))))
              (iter-safe? 1 (cdr positions) #t)))
```
程序运行的结果是给出一个list的集合，其中的每个list都是一个解，由8个数组成。其中每个元素的值表示行，元素在列表中的位置表示列。(5 7 2 6 3 1 4 8)中的5表示位置第8列的第5行。
(adjoin-position new-row k rest-of-queens)的实现方式表示每当新增一列k时，把表示这列中每一行的元素的数值new-row放在已有结果的前面，也就是说，结果给出的列的顺序是逆序的(col8 col7....col1)
(safe? k positions)：(car postions)表示第k列上的皇后的位置，检查它相对于(cdr postions)中所有的皇后是否安全。对(cdr postions)进行递归的检查，当相对于某一列的皇后不安全时，直接返回#f。n表示需要检查的列与k的距离。当检查完成时，n=k-1，同时剩余的序列为空。

### 练习2.43

> Louis Reasoner在做练习2.42时遇到了麻烦，他的queens过程看起来能行，但却运行得极慢(Louis居然无法忍耐到它解出6*6棋盘的问题)。当Louis请Eva Lu Ator帮忙时，她指出他在flatmap里交换了嵌套映射的顺序，将它写成了：
> ```lisp
> (flatmap (lambda (new-row)
>                  (map (lambda (rest-of-queens)
>                               (adjoin-position new-row k rest-of-queens))
>                       (queen-cols (- k 1))))
>          (enumerate-interval 1 board-size))
> ```
> 请解释一下，为什么这样交换顺序会使程序运行得非常慢。估计一下，用Louis的程序去解决八皇后问题大约需要多少时间，假定练习2.42中的程序需要用时间T求解这一难题。

对于2.42的程序，每执行一次flatmap，需要计算一次(queen-cols (- k 1)),计算(length (queen-cols (- k 1)))次(enumerate-interval 1 board-size)).Louis的程序则反过来，每执行一次flatmap，需要计算一次(enumerate-interval 1 board-size),计算(length (enumerate-interval 1 board-size))次(queen-cols (- k 1))。大约需要$T*{board-size}$

### 练习2.44

> 请定义出corner-split里使用的过程up-split，它与right-split类似，除了在其中交换了below和beside的角色之外。

```lisp
(define (up-split painter n)
        (if (= n 0)
            painter
            (let ((smaller (up-split painter (- n 1))))
                 (below painter (beside smaller smaller)))))
```

```lisp
(up-split painter 3)
(below p (beside s s))
(below p (beside (up-split p 2) (up-split 2)))
(below p (beside (below p (beside (up-split p 1))) (below p (beside (up-split p 1)))))
(below p (beside (below p (beside (below p (beside (up-split p 0) (up-split p 0))))) (below p (beside (below p (beside (up-split p 0) (up-split p 0)))))))
(below p (beside (below p (beside (below p (beside p p)))) (below p) beside (below p (beside p p))))
```

### 练习2.45

> 可以将right-split和up-split表述为某种广义划分操作的实例。请定义一个过程split，使他具有如下性质，求值：
> ```lisp
> (define right-split (split beside below))
> (define up-split (split below beside))
> ```
> 能够产生出过程right-split和up-split，其行为与前面定义的过程一样。

```lisp
(define (splite op1 op2)
        (lambda (painter n)
                (if (= n 0)
                    painter
                    (let ((smaller ))
                         (op1 painter (op2 smaller smaller))))))
```

### 练习2.46

> 从原点出发的一个二维向量v可以用一个由x坐标和y坐标构成的序对表示。请为这样的向量实现一个数据抽象：给出一个构造函数make-vect，以及对应的选择函数xcor-vect和ycor-vect。借助于你给出的构造函数和选择函数，实现过程add-vect、sub-vect和scale-vect，它们能完成向量加法、向量减法和向量的伸缩。
$
\begin{aligned}
&(x_1,y_1)+(x_2,y_2)=(x_1+x_2,y_1+y_2) \\
&(x_1,y_1)-(y_1,y_2)=(x_1-x_2,y_1-y_2) \\
&s*(x,y)=(sx,sy)
\end{aligned}
$

```lisp
(define (make-vect x y)
        (cons x y))

(define (xcor-vect vect)
        (car vect))

(define (ycor-vect vect)
        (cdr vect))

(define (add-vect a b)
        (make-vect (+ (xcor-vect a) (xcor-vect b))
                   (+ (ycor-vect a) (ycor-vect b))))

(define (sub-vect a b)
        (make-vect (- (xcor-vect a) (xcor-vect b))
                   (- (ycor-vect a) (ycor-vect b))))

(define (scale-vect n a)
        (make-vect (* n (xcor-vect a))
                   (* n (ycor-vect a))))
```

### 练习2.47

> 下面是实现框架的两个可能的过程函数：
> ```lisp
> (define (make-frame origin edge1 edge2)
>         (list origin edge1 edge2))
> (define (make-frame origin edge1 edge2)
>         (cons origin (cons edge1 edge2)))
> ```
> 请为每个构造函数提供适当的选择函数，为框架做出相应的实现。

```lisp
(define (make-frame origin edge1 edge2)
        (list origin edge1 edge2))

(define (origin-frame f)
        (car f))

(define (edge1-frame f))
        (car (cdr f))

(define (edge2-frame f))
        (car (cdr (cdr f)))
;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (make-frame origin edge1 edge2)
        (cons origin (cons edge1 edge2)))

(define (origin-frame f)
        (car f))

(define (edge1-frame f)
        (car (cdr f)))

(define (edge2-frame f)
        (cdr (cdr f)))
```

### 练习2.48

> 平面上的一条直线可以用一对向量表示——从原点到线段起点的向量，以及从原点到线段终点的向量。请你在练习2.46做出的向量表示定义一种线段表示，其中用构造函数make-segment以及选择函数start-segment和end-segment。

```lisp
(define (make-segment sv ev)
        (cons sv ev))

(define (start-segment s)
        (car s))

(define (end-segment s)
        (cdr s))
```

### 练习2.49

> 利用segment->painter定义下面的基本画家：
a)画出给定框架边界的画家。
b)通过连接框架两对角线画出一个大叉子的画家。
c)通过连接框架各边的中点画出一个菱形的画家。
d)画家wave。

```lisp
(define boder-painter
        (let ((point-a (make-vect 0 0))
              (point-b (make-vect 0 1))
              (point-c (make-vect 1 1))
              (point-d (make-vect 1 0)))
             (segments->painter (list (make-segment point-a point-b)
                                      (make-segment point-b point-c)
                                      (make-segment point-c point-d)
                                      (make-segment point-d point-a)))))

(define diagonal-painter
        (let ((point-a (make-vect 0 0))
              (point-b (make-vect 0 1))
              (point-c (make-vect 1 1))
              (point-d (make-vect 1 0)))
             (segments->painter (list (make-segment point-a point-c)
                                      (make-segment point-b point-d)))))

(define diamond-painter
        (let ((point-a (make-vect 0 0.5))
              (point-b (make-vect 0.5 1))
              (point-c (make-vect 1 0.5))
              (point-d (make-vect 0.5 0)))
             (segments->painter (list (make-segment point-a point-b)
                                      (make-segment point-b point-c)
                                      (make-segment point-c point-d)
                                      (make-segment point-d point-a)))))

(define wave
        (let ((point-a (make-vect 0 0.9))
              (point-b (make-vect 0.15 0.7))
              (point-c (make-vect 0.25 0.85))
              (point-d (make-vect 0.4 0.8))
              (point-e (make-vect 0.3 0.9))
              (point-f (make-vect 0.4 1))
              (point-g (make-vect 0.6 1))
              (point-h (make-vect 0.7 0.9))
              (point-i (make-vect 0.6 0.8))
              (point-j (make-vect 0.8 0.8))
              (point-k (make-vect 1 0.4))
              (point-l (make-vect 1 0.3))
              (point-m (make-vect 0.6 0.5))
              (point-n (make-vect 0.8 0))
              (point-o (make-vect 0.7 0))
              (point-p (make-vect 0.5 0.3))
              (point-q (make-vect 0.3 0))
              (point-r (make-vect 0.2 0))
              (point-s (make-vect 0.3 0.6))
              (point-t (make-vect 0.25 0.7))
              (point-u (make-vect 0.15 0.5))
              (point-v (make-vect 0 0.8)))
             (segments->painter (list (make-segment point-a point-b)
                                      (make-segment point-b point-c)
                                      (make-segment point-c point-d)
                                      (make-segment point-d point-e)
                                      (make-segment point-e point-f)
                                      (make-segment point-g point-h)
                                      (make-segment point-h point-i)
                                      (make-segment point-i point-j)
                                      (make-segment point-j point-k)
                                      (make-segment point-l point-m)
                                      (make-segment point-m point-n)
                                      (make-segment point-o point-p)
                                      (make-segment point-p point-q)
                                      (make-segment point-r point-s)
                                      (make-segment point-s point-t)
                                      (make-segment point-t point-u)
                                      (make-segment point-u point-v)))))
```

### 练习2.50

> 请定义变换flip-horiz，它能在水平方向上反转画家。再定义出对画家做反时针方向上180度和270度旋转的变换。

```lisp
(define (flip-horiz painter)
        (transform-painter painter
                           (make-vect 1 0)
                           (make-vect 0 0)
                           (make-vect 1 1)))

(define (rotate180 painter)
        (transform-painter painter
                           (make-vect 1 1)
                           (make-vect 0 1)
                           (make-vect 1 0)))

(define (rotate270 painter)
        (transform-painter painter
                           (make-vect 0 1)
                           (make-vect 1 1)
                           (make-vect 0 0)))
```

初始框架(make-frame (make-vect 0 0) (make 1 0) (make 0 1))

### 练习2.51

> 定义对画家的below操作，它以两个画家为参数。在给定了一个框架后，由below得到的画家将要求第一个画家在框架的下部画图，要求第二个画家在框架的上部画图。请按两种方式定义below：首先写出一个类似于上面beside的过程，另一个则直接通过beside和适当的旋转操作(来自练习2.50)完成有关工作。

```lisp
(define (below painter1 painter2)
        (let ((split-point (make-vect 0 0.5)))
             (let ((paint-bottom (transform-painter painter1
                                                    (make-vect 0 0)
                                                    (make-vect 1 0)
                                                    split-point)
                   (paint-top (transform-painter painter2
                                                 split-point
                                                 (make-vect 1 0.5)
                                                 (make-vect 0 1))))
                  (lambda (frame)
                          (paint-bottom frame)
                          (paint-top frame)))))

(define (below painter1 painter2)
        (rotate90 (beside (rotate270 painter1)
                          (rotate270 painter2))))
```

### 练习2.52

> 在上面描述的各个层次上工作，修改图2-9中所示的方块的限制。特别是：
a)给练习2.49的基本wave画家加入某些线段(例如，加上一个笑脸)
b)修改corner-split的构造模式(例如，只用up-split和right-split的图像的各一个副本，而不是两个)
c)修改square-limit，换一种使用square-of-four的方式，以一种不同模式组合起各个角区(例如，你可以让大的Rogers先生从正方形的每个角向外看)

a)在调用segments->painter时添加线段，即将新的线段加入segment-list

```lisp
(define (corner-split painter n)
        (if (= n 0)
            painter
            (beside (below painter (up-split (- n 1)))
                    (below (right-split (- n 1)) (corner-split (- n 1))))))

(define (square-limit painter n)
        (let ((combine4 (square-of-four identity flip-horiz
                                        flip-vert rotate180)))
             (combine4 (corner-split painter n))))  
```

### 练习2.53

> 解释器在求值下面各个表达式时将打印出什么？
> ```list
> (list 'a 'b 'c)
> (list (list 'george))
> (cdr '((x1 x2) (y1 y2)))
> (cadr '((x1 x2) (y1 y2)))
> (pair? (car '(a short list)))
> (memq 'red '((red shoes) (blue socks)))
> (memq 'red '(red shoes blue socks))
> ```

```lisp
(list 'a 'b 'c)
(a b c)
(list (list 'george))
((george))
(cdr `((x1 x2) (y1 y2)))
((y1 y2))
(pair? (car '(a short list)))
#f
(memq 'red '((red shoes) (blue socks)))
#f
(memq 'red '(red shoes blue socks))
(red shoes blue socks)
```

### 练习2.54

> 如果两个表包含同样的元素，这些元素也按同样的顺序排列，那么就称这两个表equal？。例如：
`(equal? '(this is a list) '(this is a list))`
是真，而
`(equal? '(this is a list) '(this (is a) list))`
是假。说得更准确些，我们可以从符号相等的基本eq?出发，以递归方式定义出equal？。a和b是equal?的，如果他们都是符号，而且这两个符号满足eq?；或者它们都是表，而且(car a)和(car b)相互equal?，它们的(cdr a)和(cdr b)也是equal?。请利用这一思路定义出equal?过程。

```lisp
(define (equal? a b)
        (cond ((and (pair? a) (pair? b)) 
               (and (eq? (car a) (car b)) (equal? (cdr a) (cdr b))))
              ((and (not (pair? a)) (not (pair? b))) (eq? a b))
              (else #f)))
```

### 练习2.55

> Eva Lu Ator输入了表达式：
`(car ''abracadabra)`
令她吃惊的是解释器打印出的是quote。请解释这一情况。

表达式`''abracadabra`的值是`'abracadabra`，因为解释器会将第一个单引号之后空格之前的所有字符都看作被引用的对象。
lisp引入了特殊形式quote作用与单引号一致(见**脚注100**)。因此(car ''abracadabra)等同于(car (quote (quote abracadabra)))。


```lisp
(car ''abracadabra)
(car (quote (quote abracadabra)))
(car '(quote abracadabra))
(car (list 'quote 'abracadabra))
quote
```

### 练习2.56

> 请说明如何扩充基本求导规则，以便能够处理更多种类的表达式。例如，通过给程序deriv增加一个新子句，并以适当方式定义过程exponentiation?、base、exponet和make-exponentiation的方式，实现下述求导规则(你可以考虑用符号**表示乘幂):
$
\frac {d(u^n)} {dx}=nu^{n-1}(\frac {du}{dx})
$
请将如下规则也构造到程序里：任何东西的0次幂都是1，而它们的1次幂都是其自身。

```lisp
(define (** a n)
        (expt a n))

(define (make-exponentiation a n)
        (cond ((=number? n 0) 1)
              ((=number? n 1) a)
              (else (list '** a n))))

(define (base e)
        (cadr e))

(define (exponent e)
        (caddr e))

(define (exponentiation? e)
        (and (pair? e) (eq? (car e) '**)))

(define (deriv exp var)
        (cond ((number? exp) 0)
              ((variable? exp)
               (if (same-variable? exp var)
                   1
                   0))
              ((sum? exp)
               (make-sum (deriv (addend exp) var)
                         (deriv (augend exp) var)))
              ((product? exp)
               (make-sum (make-product (multiplier exp)
                                       (deriv (multiplicand exp) var))
                         (make-product (deriv (multiplier exp) var)
                                       (multiplicand exp))))
              ((exponentiation? exp)
               (make-product (make-product (exponent exp)
                                           (make-exponentiation (base exp)
                                                                (make-sum (exponent exp) -1)))
                             (deriv (base exp) var)))
              (else (error "unknown expression type--DERIV" exp))))

```

### 练习2.57

> 请扩充求导程序，使之能够处理任意项(两项或者更多项)的和与乘积。这样，上面的最后一个例子就可以表示为：
`(deriv '(* x y (+ x 3)) 'x)`
设法通过只修改和与乘积的表示，而完全不修改过程deriv的方式完成这一扩充。例如，让一个和式的addend是它的第一项，而其augend是和式中的其余项。

```lisp
(+ a1 a2 a3)
=> (+ a1 (+ a2 a3))
```

```lisp
(define (addend s) (cadr s))

(define (augend s)
        (let ((aug (cddr s)))
             (if (null? (cdr aug))
                 (car aug)
                 (cons '+ aug))))

(define (multiplier p)
        (cadr p))

(define (multiplicand p)
        (let ((cand (cddr p)))
             (if (null? (cdr cand))
                 (car cand)
                 (cons '* (cddr p)))))
```

### 练习2.58

> 假定我们希望修改求导程序，使它能用于常规数学公式，其中+和*采用的是中缀运算符而不是前缀。由于求导程序是基于抽象数据定义的，要修改它，使之能用于另一种不同的表达式表示，我们只需要换一套工作在新的、求导程序需要使用的代数表达式的表示形式上的谓词、选择函数和构造函数。
a)请说明怎样做出这些过程，以便完成在中缀表示形式(例如$(x+(3*(x+(y+2)))))$上的代数表达式求导。为了简化有关的工作，现在可以假定+和*总是取两个参数，而且表达式中已经加上了所有的括号。
b)如果允许标准的代数写法，例如$(x+3*(x+y+2))$，问题就会变得更困难许多。在这种表达式里可能不写不必要的括号，并且嘉定乘法应该在加法之前完成，你还能为这种表示方式设计好适当的谓词、选择函数和构造函数，使我们的求导程序仍然能工作吗？

a)

```lisp
(define (make-sum a b)
        (cond ((= a 0) b)
              ((= b 0) a)
              ((and (number? a) (number? b))
               (+ a b))
              (else (list a '+ b))))

(define (sum? e)
        (and (pair? e) (eq? (cadr e) '+)))

(define (addend e)
        (car e))

(define (augend e)
        (caddr e))

(define (make-product a b)
        (cond ((= a 0) 0)
              ((= b 0) 0)
              ((and (number? a) (number? b))
               (* a b))
              (else (list a '* b))))

(define (product? e)
        (and (pair? e) (eq? (cadr e) '*)))

(define (multiplier e)
        (car e))

(define (multiplicand e)
        (caddr e))
```

b)只能用于处理+和*两种运算

```lisp
(define (make-sum a b)
        (cond ((=number? a 0) b)
              ((=number? b 0) a)
              ((and (number? a) (number? b))
               (+ a b))
              ((pair? a)
               (if (pair? b)
                   (append a (cons '+ b))
                   (append a (list '+ b))))
              (else (if (pair? b)
                        (cons a (cons '+ b))
                        (cons a (list '+ b))))))



(define (sum? e)
        (and (pair? e) (eq? (cadr e) '+)))

(define (addend e)
        (car e))

(define (augend e)
        (let ((x (cddr e)))
             (if (or (pair? (car x)) (pair? (cdr x)))
                 x
                 (car x))))


(define (make-product a b)
        (cond ((=number? a 0) 0)
              ((=number? b 0) 0)
              ((and (number? a) (number? b))
               (* a b))
              ((pair? a)
               (if (sum? a)
                   (cond ((sum? b) (list a '* b))
                         ((pair? b) (cons a (cons '* b)))
                         (else (cons a (cons '* (list b)))))
                   (cond ((sum? b) (append a (list '* b)))
                         ((pair? b) (append a (cons '* b)))
                         (else (append a (cons '* (list b)))))))
              (else (cond ((sum? b)
                           (list a '* b))
                          ((pair? b)
                           (cons a (cons '* b)))
                          (else (cons a (cons '* (list b))))))))

(define (product? e)
        (and (pair? e) (eq? (cadr e) '*)))

(define (multiplier e)
        (car e))

(define (multiplicand e)
        (let ((x (cddr e)))
             (if (or (pair? (car x)) (pair? (cdr x)))
                 (if (sum? (car x))
                     (car x)
                     x)
                 (car x))))
```

### 练习2.59

> 请为采用未排序表的集合实现定义union-set操作。

```lisp
(define (union-set set1 set2)
        (if (null? set2)
            set1
            (if (element-of-set? (car set2) set1)
                (union-set set1 (cdr set2))
                (union-set (append set1 (list (car set2)))
                           (cdr set2)))))
(define (union-set set1 set2)
        (if (null? set2)
            set1
            (union-set (adjoin-set (car set2) set1) (cdr set2))))
```

### 练习2.60

> 我们前面说明了如何将集合表示为没有重复元素的表。现在假定允许重复，例如，集合{1，2，3}可能被表示为表(2 3 2 1 3 2 2)。请为在这种表示上的操作设计过程element-of-set?、adjoin-set、union-set和intersection-set。与前面不重复表示里的相应操作相比，现在各个操作的效率怎么样？在什么样的应用中你更倾向于使用这种表示，而不是前面那种无重复的表示？

```lisp
(define (adjoin-set x set)
        (cons x set))

(define (union-set set1 set2)
        (append set1 set2))
```

(element-of-set?)(intersection-set)与原实现方式相同
(adjoin-set)和(union-set)的步数都是$\theta(1)$
**n大于等于原实现方式的n**
(element-of-set)是$\theta(n)$
(intersection-set)是$\theta(n^2)$

### 练习2.61

> 请给出采用排序表示时adjoin-set的实现。通过类似element-of-set?的方式说明，可以如何利用排序的优势得到一个过程，其平均所需的步数是采用未排序表示时的一半。

```lisp
(define (adjoin x set)
        (cond ((null? set) (list x))
              ((= x (car set)) set)
              ((< x (car set)) (cons x set))
              (else (cons (car set) (adjoin x (cdr set))))))
```

### 练习2.62

> 请给出在集合的排序表示上union-set的一个$\theta(n)$实现。

```lisp
(define (union-set set1 set2)
        (if (or (null? set1) (null? set2))
            (append set1 set2)
            (let ((x1 (car set1))
                  (x2 (car set2)))
                 (cond ((= x1 x2)
                        (cons x1 (union-set (cdr set1) (cdr set2))))
                       ((< x1 x2)
                        (cons x1 (union-set (cdr set1) set2)))
                       (else
                        (cons x2 (union-set set1 (cdr set2))))))))
```